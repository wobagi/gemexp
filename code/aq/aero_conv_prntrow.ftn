      SUBROUTINE PRNTROW (FLD,NAME,LOC,NLEV,LOFF,ILG,IL1,IL2,JLAT)
C 
C     * M.LAZARE. - AUG 13/90. ADD SECOND DIMENSION TO USE OF "FLD" FOR 
C     *                        SINGLE-LEVEL FIELDS, AS SHOULD BE. 
C     * M.LAZARE. - OCT 20/88.
C 
C     * PRINTS OUT ROW VALUES (BETWEEN IL1 AND IL2) OF ARRAY FLD, 
C     * FOR EACH OF NLEV LEVELS AT "JLAT" LATITUDE ROW INSIDE ROUTINE.
C     * 
C     *      FLD  = ARRAY TO BE PRINTED OUT.
C     *      NAME = NAME OF ARRAY TO BE INCLUDED IN PRINTOUT, 
C     *             I.E. "TH" FOR THROW.
C     *      LOC  = LOCATION WHERE PRINTOUT IS OCCURRING, I.E.
C     *             "AFTER VRTDFS". 
C     *             BEING DISPLAYED.
C     *      NLEV = NUMBER OF LEVELS FOR VARIABLE.
C     *      LOFF = NUMBER OF LEVELS (INCLUDING MOON LAYER) BEFORE
C     *             VARIABLE HAS VALUES, I.E. LOFF=MSG+1 FOR ESTG,
C     *             LOFF=0 FOR THROW, LOFF=1 FOR UROW. FOR SINGLE-
C     *             LEVEL FIELDS, SOME LEVEL INDICATOR (I.E. COULD BE 
C     *             DONE IN A VERTICAL LEVEL LOOP). 
C     *             MOON LAYER REFERENCED TO AS LEVEL "0" IN PRINTOUT.
C     *      ILG  = NUMBER OF EQUALLY-SPACED LONGITUDES TO BE PRINTED OUT.
C     *      IL1  = STARTING LONGITUDE NUMBER FOR PRINTOUT. 
C     *      IL2  = FINISHING LONGITUDE NUMBER FOR PRINTOUT.
C     *      JLAT = CURRENT LATITUDE ROW FOR PRINTOUT.
C 
      IMPLICIT REAL (A-H,O-Z),
     +INTEGER (I-N)

      REAL FLD(ILG,NLEV)
      CHARACTER*14 LOC
      CHARACTER*7 NAME
C-----------------------------------------------------------------------
C 
C     * DISTINGUISH BETWEEN SINGLE-LEVEL AND MULTI-LEVEL FIELDS.
C 
      IF(NLEV.EQ.1) THEN
             WRITE(6,6100) LOC,LOFF,NAME,JLAT,(FLD(IL,1),IL=IL1,IL2)
 6100        FORMAT ('0',A14,', LEVEL',I3,' FOR VARIABLE ', 
     1               A7,' AT LATITUDE ROW',I3,' :',/,(5(1X,1PE24.16)))
      ELSE
         LIND=LOFF-1
         WRITE(6,6200)
 6200    FORMAT ('1') 
         DO 200 L=1,NLEV
             WRITE(6,6300) LOC,L+LIND,NAME,JLAT,(FLD(IL,L),IL=IL1,IL2)
 6300        FORMAT ('0',A14,', LEVEL',I3,' FOR VARIABLE ', 
     1               A7,' AT LATITUDE ROW',I3,' :',/,(5(1X,1PE24.16)))
  200    CONTINUE 
      ENDIF 
C 
      RETURN
      END 
